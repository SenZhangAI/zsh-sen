# Path to your zsh config installation.
export ZSH=$HOME/.zsh-sen
# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files, except zsh-syntax-highlighting
for file in ${${(M)config_files:#*/path.zsh}:#*/zsh-syntax-highlighting/*}
do
  source $file
done

# load everything but the path and completion files and zsh-syntax-highlighting
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/zsh-syntax-highlighting/*}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit && compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
